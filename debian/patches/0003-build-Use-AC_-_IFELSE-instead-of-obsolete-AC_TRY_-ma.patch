From 935352bc28cd26d73a72cb677eff68633b9ba467 Mon Sep 17 00:00:00 2001
From: Guillem Jover <guillem@hadrons.org>
Date: Sat, 28 Aug 2021 00:54:31 +0200
Subject: [PATCH 3/4] build: Use AC_*_IFELSE instead of obsolete AC_TRY_*
 macros

---
 am/check_weak_refs.m4 | 18 +++++++++---------
 am/krb5.m4            | 16 ++++++++++++----
 am/libcurses.m4       |  4 +++-
 configure.ac          | 13 ++++++++-----
 4 files changed, 32 insertions(+), 19 deletions(-)

diff --git a/am/check_weak_refs.m4 b/am/check_weak_refs.m4
index 6256acda..fa3fe037 100644
--- a/am/check_weak_refs.m4
+++ b/am/check_weak_refs.m4
@@ -32,9 +32,9 @@ AC_DEFUN([IU_CHECK_WEAK_REFS], [
   AH_TEMPLATE(HAVE_WEAK_REFS, 1, [Define if you have weak references])
   AC_CACHE_CHECK(whether gcc weak references work,
 		 inetutils_cv_attr_weak_refs,
-    AC_TRY_LINK([],
-      [extern char *not_defined (char *, char *) __attribute__ ((weak));
-	if (not_defined) puts ("yes"); ],
+    AC_LINK_IFELSE([AC_LANG_PROGRAM([],
+	[[extern char *not_defined (char *, char *) __attribute__ ((weak));
+	if (not_defined) puts ("yes"); ]])],
       [inetutils_cv_attr_weak_refs=yes],
       [inetutils_cv_attr_weak_refs=no]))
   if test "$inetutils_cv_weak_refs" = yes; then
@@ -44,10 +44,10 @@ AC_DEFUN([IU_CHECK_WEAK_REFS], [
   else
     AC_CACHE_CHECK(whether pragma weak references work,
 		   inetutils_cv_pragma_weak_refs,
-      AC_TRY_LINK([],
-	[extern char *not_defined (char *, char *);
+      AC_LINK_IFELSE([AC_LANG_PROGRAM([],
+	[[extern char *not_defined (char *, char *);
 #pragma weak not_defined
-	 if (not_defined) puts ("yes"); ],
+	 if (not_defined) puts ("yes"); ]])],
 	[inetutils_cv_pragma_weak_refs=yes],
 	[inetutils_cv_pragma_weak_refs=no]))
     if test "$inetutils_cv_pragma_weak_refs" = yes; then
@@ -57,10 +57,10 @@ AC_DEFUN([IU_CHECK_WEAK_REFS], [
     else
       AC_CACHE_CHECK(whether asm weak references work,
 		     inetutils_cv_asm_weak_refs,
-	AC_TRY_LINK([],
-	  [extern char *not_defined (char *, char *);
+	AC_LINK_IFELSE([AC_LANG_PROGRAM([],
+	  [[extern char *not_defined (char *, char *);
 	   asm (".weak not_defined");
-	   if (not_defined) puts ("yes"); ],
+	   if (not_defined) puts ("yes"); ]])],
 	  [inetutils_cv_asm_weak_refs=yes],
 	  [inetutils_cv_asm_weak_refs=no]))
       if test "$inetutils_cv_asm_weak_refs" = yes; then
diff --git a/am/krb5.m4 b/am/krb5.m4
index 676ffecd..773f925b 100644
--- a/am/krb5.m4
+++ b/am/krb5.m4
@@ -67,14 +67,18 @@ AC_DEFUN([IU_CHECK_KRB5],
 
     ## Check for new MIT kerberos V support
     LIBS="$saved_LIBS -lkrb5 -lk5crypto -lcom_err"
-    AC_TRY_LINK([], [return krb5_init_context((void *) 0); ],
+    AC_LINK_IFELSE(
+      [AC_LANG_PROGRAM([],
+        [[return krb5_init_context((void *) 0); ]])],
       [KRB5_IMPL="MIT"
        KRB5_LIBS="$KRB5_LDFLAGS -lkrb5 -lk5crypto -lcom_err"], )
 
     ## Heimdal kerberos V support
     if test "$KRB5_IMPL" = "none"; then
       LIBS="$saved_LIBS -lkrb5 -ldes -lasn1 -lroken -lcrypt -lcom_err"
-      AC_TRY_LINK([], [return krb5_init_context((void *) 0); ],
+      AC_LINK_IFELSE(
+        [AC_LANG_PROGRAM([],
+          [[return krb5_init_context((void *) 0); ]])],
         [KRB5_IMPL="Heimdal"
          KRB5_LIBS="$KRB5_LDFLAGS -lkrb5 -ldes -lasn1 -lroken -lcrypt -lcom_err"]
          , )
@@ -88,7 +92,9 @@ AC_DEFUN([IU_CHECK_KRB5],
     ## OpenBSD variant of Heimdal
     if test "$KRB5_IMPL" = "none"; then
       LIBS="$saved_LIBS -lkrb5 -lcrypto"
-      AC_TRY_LINK([], [return krb5_init_context((void *) 0); ],
+      AC_LINK_IFELSE(
+        [AC_LANG_PROGRAM([],
+          [[return krb5_init_context((void *) 0); ]])],
         [KRB5_IMPL="OpenBSD-Heimdal"
 	 KRB5_CFLAGS="-I/usr/include/kerberosV"
          KRB5_LIBS="$KRB5_LDFLAGS -lkrb5 -lcrypto -lasn1 -ldes"], )
@@ -99,7 +105,9 @@ AC_DEFUN([IU_CHECK_KRB5],
     ## -lk5crypto. This may conflict with OpenSSL.
     if test "$KRB5_IMPL" = "none"; then
       LIBS="$saved_LIBS -lkrb5 -lcrypto -lcom_err"
-      AC_TRY_LINK([], [return krb5_init_context((void *) 0); ],
+      AC_LINK_IFELSE(
+        [AC_LANG_PROGRAM([],
+          [[return krb5_init_context((void *) 0); ]])],
         [KRB5_IMPL="OldMIT"
          KRB5_LIBS="$KRB5_LDFLAGS $KRB4_LIBS -lkrb5 -lcrypto -lcom_err"], )
     fi
diff --git a/am/libcurses.m4 b/am/libcurses.m4
index 05f54120..7c20aa79 100644
--- a/am/libcurses.m4
+++ b/am/libcurses.m4
@@ -175,7 +175,9 @@ AC_DEFUN([IU_LIB_CURSES], [
       AC_CACHE_CHECK(whether curses needs $LIBTERMCAP,
 		     inetutils_cv_curses_needs_termcap,
 	LIBS="$LIBCURSES"
-	AC_TRY_LINK([#include <curses.h>], [initscr ();],
+	AC_LINK_IFELSE(
+		[AC_LANG_PROGRAM(
+		    [[#include <curses.h>]], [[initscr ();]])],
 		    [inetutils_cv_curses_needs_termcap=no],
 		    [inetutils_cv_curses_needs_termcap=yes]))
       if test $inetutils_cv_curses_needs_termcap = yes; then
diff --git a/configure.ac b/configure.ac
index 450270d8..fd7856b9 100644
--- a/configure.ac
+++ b/configure.ac
@@ -590,9 +590,11 @@ if test "$with_wrap" = yes; then
   AC_MSG_CHECKING(hosts_ctl in -lwrap);
   save_LIBS=$LIBS
   LIBS="$save_LIBS -lwrap"
-  AC_TRY_LINK([int allow_severity = 1; int deny_severity = 1;],
-              hosts_ctl(), eval "ac_cv_lib_wrap_hosts_ctl=yes",
-              eval "ac_cv_lib_wrap_hosts_ctl=no")
+  AC_LINK_IFELSE(
+    [AC_LANG_PROGRAM([[int allow_severity = 1; int deny_severity = 1;]],
+      [[hosts_ctl()]])],
+    [eval "ac_cv_lib_wrap_hosts_ctl=yes"],
+    [eval "ac_cv_lib_wrap_hosts_ctl=no"])
   LIBS=$save_LIBS
   if test "$ac_cv_lib_wrap_hosts_ctl" = yes \
       && test "$ac_cv_header_tcpd_h" = yes; then
@@ -896,8 +898,9 @@ AC_CHECK_DECLS(htons, , ,
 # See if we have h_errno (the test is here so we can use -lresolv if
 # necessary).
 AC_CACHE_CHECK([for h_errno], inetutils_cv_var_h_errno,
-  AC_TRY_COMPILE([#include <netdb.h>],
-    [ extern int h_errno; int iu_x = h_errno; ],
+  AC_COMPILE_IFELSE(
+    [AC_LANG_PROGRAM([[#include <netdb.h>]],
+      [[ extern int h_errno; int iu_x = h_errno; ]])],
     inetutils_cv_var_h_errno=yes, inetutils_cv_var_h_errno=no))
 if test "$inetutils_cv_var_h_errno" = yes; then
   # FIXME: Isn't this wrong? Shouldn't the declaration check define
-- 
2.33.0

